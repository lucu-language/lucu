##
# everything in this file is always in scope
##

type never = !

##
# panics with message if the assertion is not met
# preferred to be used only for debugging
##
fun assert(all bool, msg str) {
	if all {
	} else { # fails
		panic(msg)
	}
}

##
# modulo operator
##
fun mod(dividend int, divisor int) int {
	dividend - ((dividend / divisor) * divisor)
}

##
# yield
##
effect yield(`t) {
	fun yield(mut t `t)
}

effect generator(`t) {
	fun generate() / yield(`t)
}

fun first(generate impl generator(`t)) `t / srcloc {
	try {
		generate.() with ({ t -> break t } as impl yield(`t) -> `t)
		panic("nothing was yielded")
	}
}

fun nth(num usize, generate impl generator(`t)) `t / srcloc {
	try {
		mut current usize = 0
		generate.() with ({
			t ->
			if current == num {
				break t
			} else {
				do current++
			}
		} as impl yield(`t) -> `t)
		panic("not enough was yielded")
	}
}

fun for(start usize, end usize, yield impl yield(usize)) {
	mut n = start
	try loop {
		if n == end {
			break
		} else {
			yield.(n++)
		}
	}
}

##
# while loop
##
fun while(test impl test, body impl run) {
	try loop {
		if test.() {
			body.()
		} else {
			break
		}
	}
}

##
# until loop
##
fun until(test impl test, body impl run) {
	try loop {
		if test.() {
			break
		} else {
			body.()
		}
	}
}

##
# repeat loop
##
fun repeat(body impl run -> `t) `t {
	try loop {
		body.()
	}
}

effect test {
	fun test() bool
}

effect run {
	fun run()
}

effect fail {
	fun fail() !
}

# TODO: && operator
fun and(a bool, b bool) bool {
	if a {
		b
	} else {
		false
	}
}

##
# exits the program and displays an error message
# preferred to be used only for debugging
##
fun panic(msg str) ! / srcloc {
	trace("panic: ")
	trace(msg)
	trace("\n")
	trap()
}

fun trap() ! / srcloc {
	trace("trapped at ")
	trace(source_location())
	trace("\n")
	trap_silent()
}

##
# exits the program and displays an error message
# marks code paths that aren't implemented yet
##
fun todo(msg str) ! / srcloc {
	trace("todo: ")
	trace(msg)
	trace("\n")
	trap()
}

fun internal() `t {
	# internal()
	loop {}
}

##
# the following functions have internal definitions
# these are not based on a specific target
##

##
# marks that a code path is unreachable
# this function should NEVER actually be called
# for displaying an error, &panic is preferred
##
fun unreachable() ! {
	internal()
}

##
# returns the length of a slice
##
fun len(slice []`elem) usize {
	internal()
}

##
# creates a slice from a pointer and length
##
fun raw_slice(ptr ^`t, len usize) []`t {
	internal()
}

##
# effect to get the location of the call in the source code
# has an internally defined global handler
# when used directly, it returns the location of the call to &source_location
# when used as an effect parameter inside a function, it returns the location of the caller
##
effect srcloc {
	fun source_location() str
}

impl srcloc {
	fun source_location() str {
		internal()
	}
}

##
# links to a dynamic library using an effect as a foreign interface
# &`effect MUST be annotated with @foreign,
# as it uses the data inside to determine the symbol names for the effect functions
##
effect foreign {
	fun impl_link(`lib str) impl `effect
	fun impl_asm(`assembly str, `constraints str, `sideeffects bool) impl `effect
	fun syscall(nr int, args [`N]int) int
}

##
# the following functions may have an internal definition based on the target
# they also have default implementations for unknown targets
##

##
# prints a debug message to the environment (usually the standard error)
# the message is printed exactly as it is, so concatinating strings is possible by calling &trace multiple times
##
fun trace(s str) {
	internal()
}

##
# function that ends execution of the entire program
# the way it ends execution depends on the target
# for instance, it could:
#  - end the process
#  - run a halt instruction
#  - throw an exception / interrupt
#  - spin forever
##
fun trap_silent() ! {
	internal()
}
